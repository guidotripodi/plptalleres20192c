import Data.List
import Test.HUnit

data Proposition = Var String | Not Proposition | And Proposition Proposition | Or Proposition Proposition | Impl Proposition Proposition deriving Eq

type Assignment = String -> Bool

-- recProp :: 
recProp  :: (String -> c) ->  					-- recVar
		(Proposition -> c -> c)-> 			-- recNot
		(Proposition -> Proposition -> c -> c -> c) ->  -- recAnd
		(Proposition -> Proposition -> c -> c -> c)->   -- recOr
		(Proposition -> Proposition -> c -> c -> c)->   -- recImpl
		Proposition -> 					-- prop
		c 
recProp recVar recNot recAnd recOr recImpl prop = case prop of Var str -> recVar str
                                                               Not p -> recNot p (rec p)
                                                               And p1 p2 -> recAnd p1 p2 (rec p1) (rec p2)
                                                               Or p1 p2 -> recOr p1 p2 (rec p1) (rec p2)
                                                               Impl p1 p2 -> recImpl p1 p2 (rec p1) (rec p2)
							where rec = recProp recVar recNot recAnd recOr recImpl

foldProp :: (String -> c) -> 					-- fVar
		(c -> c) -> 					-- fNot
		(c -> c -> c) -> 				-- fAnd
		(c -> c -> c) -> 				-- fOr
		(c -> c -> c) -> 				-- fImpl
		Proposition -> 					-- prop
		c
foldProp fVar fNot fAnd fOr fImpl prop = case prop of Var b -> fVar b
                                                      Not p1 -> fNot (rec p1)
                                                      And p1 p2 -> fAnd (rec p1)(rec p2)
                                                      Or p1 p2 -> fOr (rec p1)(rec p2)
                                                      Impl p1 p2 -> fImpl (rec p1) (rec p2)
						  where rec = foldProp fVar fNot fAnd fOr fImpl


instance Show Proposition where
  show  = foldProp id (\x -> "\172"++x) (\x y -> "("++x++" \8743 "++y++")") (\x y -> "("++ x++" \8744 "++y++")") (\x y -> "("++x++" \8835 "++y++")")
     --Códigos Unicode para simbolitos por si hay problemas de codificación:  \172, \8835, \8743, \8744.

assignTrue :: [String] -> Assignment
assignTrue ls x = elem x ls -- flip elem

eval :: Assignment -> Proposition -> Bool
eval val = foldProp (\x -> val x) not (&&) (||) (\x y -> not (x && (not y)))

elimImpl :: Proposition -> Proposition
elimImpl = foldProp Var Not And Or (\x y -> Or (Not x) y)

negateProp :: Proposition -> Proposition
negateProp = recProp (\x -> Not (Var x)) (\x recX -> x) (\x y recX recY -> Or recX recY) (\x y recX recY -> And recX recY) (\x y recX recY -> And x recY)

nnf :: Proposition -> Proposition
nnf xs = foldProp Var (\x -> negateProp x) And Or Impl (elimImpl xs)

vars :: Proposition -> [String]
vars = recProp (\x -> [x]) (\x recX -> recX) (\x y recX recY -> union recX recY) (\x y recX recY -> union recX recY) (\x y recX recY -> union recX recY)
--fold

parts :: [a] -> [[a]]
parts xs = reverse (foldr (\x res -> res ++ (map (x:) res)) [[]] xs)

sat :: Proposition -> [[String]]
sat x = filter (\y ->  (eval (assignTrue y) x)) (parts (vars x)) 

satisfiable :: Proposition -> Bool
satisfiable x = length( sat x) > 0 

tautology :: Proposition -> Bool
tautology x = (length (parts (vars x))) == (length (sat x))  

equivalent :: Proposition -> Proposition -> Bool
equivalent x y  =  filter (\z -> eval (assignTrue z) y ) (sat x) == (sat x) &&
                  filter (\z -> eval (assignTrue z) x ) (sat y) == (sat y)

-- Proposiciones de prueba --

f1 = Impl (Var "p") (Var "q")
f2 = Not(Impl (Var "p") (Var "q"))
f3 = Not (Var "p")
f4 = Or f1 f2
f5 = Impl (Var "r") (Var "r")


-- Tests
main :: IO Counts
main = do runTestTT allTests

allTests = test [
  "ejercicio1" ~: testsEj1,
  "ejercicio2" ~: testsEj2,
  "ejercicio3" ~: testsEj3,
  "ejercicio4" ~: testsEj4,
  "ejercicio5" ~: testsEj5,
  "ejercicio6" ~: testsEj6
  ]

testsEj1 = test [
  5 ~=? foldProp (const 1) (+1) (+) (+) (+) f4
  ]
  
testsEj2 = test [
  "\172(p \8835 q)"~=? show f2
  ]

testsEj3 = test [
  False ~=?  eval ("p" ==) (Not (Var "p"))
  ]

testsEj4 = test [
  True ~=?  eval (assignTrue ["p","q"]) (Impl (And (Var "p") (Var "r")) (And (Not (Var "q")) (Var "q"))),
  False ~=?  eval ("p" ==) (Not (Var "p"))
  ]

testsEj5 = test [
  Var "q" ~=?  negateProp (Not $ Var "q"),
  Or (Not (And (Var "p") (Var "q"))) (Var "h") ~=?  elimImpl (Impl (And (Var "p") (Var "q")) (Var "h")),
  (And (Not(Var "p")) (Not (Var "q"))) ~=? negateProp (Impl (Not (Var "p")) (Var "q")),
  (Or (Not(Var "p")) (Not (Var "q"))) ~=? nnf (Not (And (Var "p") (Var "q"))),
  Var "p" ~=?  nnf (Not $ Not $ Var "p")
  ]

testsEj6 = test [
  [[1,2,3],[1,2],[1,3],[1],[2,3],[2],[3],[]] ~=? parts [1,2,3],
  ["p"]~=? vars  (Impl (Var "p") (Var "p")),
  ["p","q"] ~=? vars  (Impl (Var "p") (Var "q")),
  [["p","w"],["p"],["w"]] ~=? sat (Or (Var "p") (Var "w")),
  [["w"]] ~=? sat (And (Not (Var "p")) (Var "w")),
  True ~=? satisfiable  (And (Var "p") (Var "w")),
  True ~=? satisfiable  (And (Not (Var "p"))( Not (Var "w"))), 
  False ~=? tautology  (And (Var "p") (Not (Var "q"))),
  True ~=? tautology  (Or (Var "p") (Not (Var "p"))),
  False ~=? equivalent  (And (Impl (Var "p") (Var "p")) (Var "q")) (Var "j"), --Cambiar esto por tests verdaderos.
  True ~=? equivalent  (And (Impl (Var "p") (Var "p")) (Var "q")) (Var "q") --Cambiar esto por tests verdaderos.
  ]

